prototype(Community.News:CategoryDetail) < prototype(TYPO3.Neos:Content) {
    // set the template path and section to use
    templatePath = 'resource://Community.News/Private/Templates/TypoScriptObjects/CategoryDetail.html'
    sectionName = 'categoryDetail'

    title = ${q(node).property('title')}

    newsByCategory = Community.News:AbstractList {
        @context.listTitle = 'News with this category'
        prototype(Flowpack.Listable:Listable) {
            @context.paginationEnabled = false
            collection = ${q(site).find('[instanceof Community.News:AbstractNews]').filterByReference('categories', node).get()}
        }
    }

    main = TYPO3.Neos:ContentCollection {
        nodePath = 'main'
    }
}

prototype(TYPO3.Neos:PrimaryContent) {
    category{
        @position = 'before default'
        condition = ${q(node).is('[instanceof Community.News:Category]')}
        type = 'Community.News:CategoryDetail'
    }
}
