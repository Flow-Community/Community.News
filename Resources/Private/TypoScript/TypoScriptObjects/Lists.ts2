prototype(Community.News:AbstractList) < prototype(Flowpack.Listable:List) {
    prototype(Flowpack.Listable:Listable) {
        @context.sortProperty = 'created'
        # filterByTag - this considers the GET parameter (e.g.: &tag=mysearchtag) - if there is no argument - all news are shown
        collection.@process.filterByTag = ${request.arguments.tag != null ? q(value).filter('[tags *= ' + request.arguments.tag + ']').get() : value}
    }
    @cache {
        mode = 'cached'
        entryIdentifier.pagination = ${request.pluginArguments.listable-paginate.currentPage}
        entryTags {
            1 = ${'NodeType_Community.News:AbstractNews'}
        }
    }
}

prototype(Community.News:NewsList) < prototype(Community.News:AbstractList) {
    @context.listTitle = 'News'
    prototype(Flowpack.Listable:Listable) {
        @context.itemsPerPage = 5
        @context.maximumNumberOfLinks = 10
    }
}

prototype(Community.News:NewsLatest) < prototype(Community.News:AbstractList) {
    @context.listTitle = 'Latest news'
    prototype(Flowpack.Listable:Listable) {
        @context.paginationEnabled = false
    }
}

prototype(TYPO3.Neos:ContentCollection).@cache.entryIdentifier.pagination = ${request.pluginArguments.listable-paginate.currentPage}
prototype(TYPO3.Neos:Page).@cache.entryIdentifier.pagination = ${request.pluginArguments.listable-paginate.currentPage}
root.@cache.entryIdentifier.pagination = ${request.pluginArguments.listable-paginate.currentPage}
